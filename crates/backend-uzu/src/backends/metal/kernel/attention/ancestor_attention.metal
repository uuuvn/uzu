#include <metal_simdgroup>
#include <metal_stdlib>
#include "../common/defines.h"
#include "../common/integral_constant.h"
#include "../common/dsl.h"
#include "../common/thread_context.h"
#include "../weaver/weaver_frontier.h"

using namespace metal;

#define SIMD_GROUPS_PER_THREADGROUP 4
#define THREADS_PER_THREADGROUP (SIMD_GROUPS_PER_THREADGROUP * METAL_SIMD_SIZE)

constant uint VALUES_PER_VECTOR = 4;
constant uint QKV_COMPONENTS = 3;
constant uint KEY_COMPONENT = 1;
constant uint VALUE_COMPONENT = 2;

template <ushort UNROLL_COUNT, typename GetPtrBaseFn>
METAL_FUNC void attend_qkv(
    float4 query,
    uint key_offset,
    uint value_offset,
    thread float4& values,
    thread float& max_score,
    thread float& sum,
    GetPtrBaseFn get_ptr_base
) {
  float4 keys[UNROLL_COUNT];
  float4 position_values[UNROLL_COUNT];
  uzu::const_for_loop<0, UNROLL_COUNT, 1>([&](auto position) {
    const device bfloat4* vectors = get_ptr_base(position);
    keys[position] = float4(vectors[key_offset]);
    position_values[position] = float4(vectors[value_offset]);
  });
  float scores[UNROLL_COUNT];
  uzu::const_for_loop<0, UNROLL_COUNT, 1>([&](auto position) {
    scores[position] = simd_sum(dot(query, keys[position]));
  });
  float new_max = max_score;
  uzu::const_for_loop<0, UNROLL_COUNT, 1>([&](auto position) { new_max = max(new_max, scores[position]); });
  const float old_factor = fast::exp(max_score - new_max);
  sum *= old_factor;
  values *= old_factor;
  uzu::const_for_loop<0, UNROLL_COUNT, 1>([&](auto position) {
    const float factor = fast::exp(scores[position] - new_max);
    sum += factor;
    values += factor * position_values[position];
  });
  max_score = new_max;
}

template <uint HEAD_DIM>
VARIANTS(HEAD_DIM, 128)
PUBLIC KERNEL(AncestorAttention)(
    const device bfloat* prefix_kv,
    device bfloat* node_kv,
    const device bfloat* current_qkv,
    const device float* cosines,
    const device float* sines,
    const device uint* node_metadata,
    const device uint* ancestor_indices,
    const device uint* ancestor_counts,
    const device uint* node_indices,
    device bfloat* output,
    constant uint& rows,
    constant uint& prefix_length,
    constant uint& ancestor_stride,
    constant uint& node_capacity,
    constant uint& max_depth,
    constant float& scale,
    const uint num_heads SPECIALIZE,
    const ThreadContext thread_context,
    const uint group GROUPS((rows * num_heads).div_ceil(SIMD_GROUPS_PER_THREADGROUP)),
    const uint lid THREADS(THREADS_PER_THREADGROUP)
) {
  const uint row_head = group * SIMD_GROUPS_PER_THREADGROUP + thread_context.simdgroup_index;
  if (row_head >= rows * num_heads) {
    return;
  }

  constexpr uint vectors_per_head = HEAD_DIM / VALUES_PER_VECTOR;
  constexpr ushort unroll_count = 4;
  const uint row = row_head / num_heads;
  const uint head = row_head % num_heads;
  const uint lane = thread_context.simd_lane_id;
  const uint model_vectors = num_heads * vectors_per_head;
  const uint qkv_vectors = QKV_COMPONENTS * num_heads * vectors_per_head;
  const uint head_offset = head * vectors_per_head + lane;
  const uint current_key_offset = KEY_COMPONENT * model_vectors + head_offset;
  const uint current_value_offset = VALUE_COMPONENT * model_vectors + head_offset;
  const uint prefix_value_offset = prefix_length * model_vectors + head_offset;
  const uint node_value_offset = node_capacity * model_vectors + head_offset;
  const device bfloat4* prefix_vectors = (const device bfloat4*)prefix_kv;
  device bfloat4* node_vectors = (device bfloat4*)node_kv;
  const device bfloat4* current_row = (const device bfloat4*)current_qkv + row * qkv_vectors;

  // Rotate this node's query and key by its position, here rather than in a
  // pass of its own. The rotation pairs every channel with one from the other
  // half of the head, and a thread 16 lanes over already holds that half, so we
  // ask it directly instead of reading the buffer a second time.
  static_assert(vectors_per_head == METAL_SIMD_SIZE, "one lane per head vector");
  const uint depth = node_metadata[uint(MetadataIdx::Depth) * rows + row];
  const uint rope_position = depth + 1u;
  const device float4* cosine_row = (const device float4*)(cosines + rope_position * HEAD_DIM);
  const device float4* sine_row = (const device float4*)(sines + rope_position * HEAD_DIM);
  const float4 cosine = cosine_row[lane];
  const float4 sine = sine_row[lane];
  const bool low_half = lane < (vectors_per_head / 2);

  const float4 raw_query = float4(current_row[head_offset]);
  const float4 paired_query = simd_shuffle_xor(raw_query, ushort(vectors_per_head / 2));
  const float4 rotated_query =
      low_half ? (raw_query * cosine - paired_query * sine) : (raw_query * cosine + paired_query * sine);

  const float4 raw_key = float4(current_row[current_key_offset]);
  const float4 paired_key = simd_shuffle_xor(raw_key, ushort(vectors_per_head / 2));
  const float4 rotated_key = low_half ? (raw_key * cosine - paired_key * sine) : (raw_key * cosine + paired_key * sine);

  const float4 query = rotated_query * scale;

  // prefix_length >= 1 (the u0 token): seed the online softmax from position 0.
  float max_score = simd_sum(dot(query, float4(prefix_vectors[head_offset])));
  float4 values = float4(prefix_vectors[prefix_value_offset]);
  float sum = 1.0f;

  uint position = 1;
  for (; position + unroll_count - 1 < prefix_length; position += unroll_count) {
    attend_qkv<unroll_count>(query, head_offset, prefix_value_offset, values, max_score, sum, [&](int step) {
      return prefix_vectors + (position + step) * model_vectors;
    });
  }
  for (; position < prefix_length; ++position) {
    attend_qkv<1>(query, head_offset, prefix_value_offset, values, max_score, sum, [&](int) {
      return prefix_vectors + position * model_vectors;
    });
  }

  const uint ancestor_count = ancestor_counts[row];
  const device uint* row_ancestors = ancestor_indices + row * ancestor_stride;
  uint offset = 0;
  for (; offset + unroll_count - 1 < ancestor_count; offset += unroll_count) {
    attend_qkv<unroll_count>(query, head_offset, node_value_offset, values, max_score, sum, [&](int step) {
      return node_vectors + row_ancestors[offset + step] * model_vectors;
    });
  }
  for (; offset < ancestor_count; ++offset) {
    attend_qkv<1>(query, head_offset, node_value_offset, values, max_score, sum, [&](int) {
      return node_vectors + row_ancestors[offset] * model_vectors;
    });
  }

  const float own_score = simd_sum(dot(query, rotated_key));
  const float4 own_value = float4(current_row[current_value_offset]);
  const float own_max = max(max_score, own_score);
  const float rescale = fast::exp(max_score - own_max);
  const float own_factor = fast::exp(own_score - own_max);
  sum = sum * rescale + own_factor;
  values = values * rescale + own_factor * own_value;
  max_score = own_max;

  if (node_capacity > 0u) {
    const uint node = node_indices[row];

    node_vectors[node * model_vectors + head_offset] = bfloat4(rotated_key);
    node_vectors[node_value_offset + node * model_vectors] = current_row[current_value_offset];
  }

  ((device bfloat4*)output)[(row * num_heads + head) * vectors_per_head + lane] = bfloat4(values / sum);
}
